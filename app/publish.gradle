apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'me.orbitalno11.lib'
def LIB_ARTIFACT_ID = 'qr-scanner'
def LIB_VERSION = '0.0.1'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            def propsFile = rootProject.file('github.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/orbitalno11/qrcode-scanner")
            credentials {
                username props['username']
                password props['token']
            }
        }
    }

    publications {
        scannerModule(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact sourceJar
            artifact "$buildDir/outputs/aar/app-release.aar"

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

